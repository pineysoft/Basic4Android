Version=3.2
IconFile=
NumberOfModules=0
Package=flm.b4a.lgdxtest
DoNotOverwriteManifest=False
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.basic4ppc.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="7" android:targetSdkVersion="14"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~'End of default text.~\n~SetActivityAttribute(Main, android:hardwareAccelerated, "true")~\n~
UserTypesHint=
NumberOfFiles=0
NumberOfLibraries=2
Library1=core
Library2=libgdx
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: lGDX Varying Friction
	#VersionCode: 1
	#VersionName: 
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: unspecified
	#CanInstallToExternalStorage: False
#End Region

#Region  Activity Attributes 
	#FullScreen: true
	#IncludeTitle: false
#End Region

Sub Process_Globals
End Sub

Sub Globals
	Dim lGdx As LibGDX
	Dim GL As lgGL
	Dim Camera As lgOrthographicCamera
	Dim World As lgBox2DWorld
	Dim Renderer As lgBox2DDebugRenderer
End Sub

Sub Activity_Create(FirstTime As Boolean)
	'Disables the accelerometer and the compass
	Dim Config As lgConfiguration
	Config.useAccelerometer = False
	Config.useCompass = False

	'Creates the LibGDX surface
	lGdx.Initialize2(Config, "LG")
End Sub

Sub Activity_Resume
	'Informs libGDX of Resume events
	If lGdx.IsInitialized Then lGdx.Resume
End Sub

Sub Activity_Pause (UserClosed As Boolean)
	'Informs libGDX of Pause events
	If lGdx.IsInitialized Then lGdx.Pause
End Sub

Sub LG_Create
	'Debug renderer
	Renderer.Initialize2(True, False, False, True, False, False)

	'World
	Dim vGravity As lgMathVector2
	World.Initialize(vGravity.set(0, -10), True, "")
	World.SetContinuousPhysics(True)
	World.SetWarmStarting(True)

	'Ground
	Dim bd As lgBox2DBodyDef
	Dim ground As lgBox2DBody = World.createBody(bd)
	Dim eShape As lgBox2DEdgeShape
	Dim V1, V2 As lgMathVector2
	eShape.set(V1.set(-40, 0), V2.set(40, 0))
	ground.createFixture2(eShape, 0)
	eShape.dispose

	'Slopes
	Dim pShape As lgBox2DPolygonShape
	pShape.setAsBox(13, 0.25)
	Dim bd As lgBox2DBodyDef
	bd.position.set(-4, 22)
	bd.angle = -0.25
	Dim ground As lgBox2DBody = World.createBody(bd)
	ground.createFixture2(pShape, 0)
	pShape.dispose

	Dim pShape As lgBox2DPolygonShape
	pShape.setAsBox(0.25, 1)
	Dim bd As lgBox2DBodyDef
	bd.position.set(10.5, 19)
	Dim ground As lgBox2DBody = World.createBody(bd)
	ground.createFixture2(pShape, 0)
	pShape.dispose

	Dim pShape As lgBox2DPolygonShape
	pShape.setAsBox(13, 0.25)
	Dim bd As lgBox2DBodyDef
	bd.position.set(4, 14)
	bd.angle = 0.25
	Dim ground As lgBox2DBody = World.createBody(bd)
	ground.createFixture2(pShape, 0)
	pShape.dispose

	Dim pShape As lgBox2DPolygonShape
	pShape.setAsBox(0.25, 1)
	Dim bd As lgBox2DBodyDef
	bd.position.set(-10.5, 11)
	Dim ground As lgBox2DBody = World.createBody(bd)
	ground.createFixture2(pShape, 0)
	pShape.dispose

	Dim pShape As lgBox2DPolygonShape
	pShape.setAsBox(13, 0.25)
	Dim bd As lgBox2DBodyDef
	bd.position.set(-4, 6)
	bd.angle = -0.25
	Dim ground As lgBox2DBody = World.createBody(bd)
	ground.createFixture2(pShape, 0)
	pShape.dispose

	'Boxes
	Dim pShape As lgBox2DPolygonShape
	pShape.setAsBox(0.5, 0.5)

	Dim fd As lgBox2DFixtureDef
	fd.shape = pShape
	fd.Density = 25

	Dim friction(5) As Float = Array As Float (0.8, 0.6, 0.4, 0.2, 0)

	For i = 0 To 4
		Dim bd As lgBox2DBodyDef
		bd.Type = World.BODYTYPE_Dynamic
		bd.position.set(-15 + 4 * i, 28)
		Dim body As lgBox2DBody = World.createBody(bd)
		fd.friction = friction(i)
		body.createFixture(fd)
	Next
	pShape.dispose
End Sub

Sub LG_Resize(Width As Int, Height As Int)
	'Sets the camera viewport and position
	Camera.Initialize2(Width / 16, Height / 16)
	Camera.Position.set(0, Height / 40, 0)
End Sub

Sub LG_Render
	'Clears the screen
	GL.glClear(GL.GL10_COLOR_BUFFER_BIT)

	'Updates the matrices of the camera
	Camera.Update

	'Updates and draws the simulation
	LG_Update
	Renderer.Render(World.InternalObject, Camera.Combined)
End Sub

Sub LG_Update
	World.Step(1/60, 8, 3)
End Sub

Sub LG_Resume
End Sub

Sub LG_Pause
End Sub

Sub LG_Dispose
	'Disposes all resources
	Renderer.dispose
	World.dispose
End Sub
